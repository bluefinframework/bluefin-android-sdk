apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/bluefinframework/bluefin-android-sdk/'
def gitUrl = 'https://github.com/bluefinframework/bluefin-android-sdk.git'
version = '1.0.12'
group = 'cn.saymagic'

configurations {
    deployerJars
}
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    resourcePrefix "bluefinrx"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 2
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "io.reactivex:rxjava:1.0.3"
    provided 'cn.saymagic:bluefinsdk:1.0.12'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                description 'Android SDK for Bluefin server to support RxJava'
                url siteUrl
                developers {
                    developer {
                        id 'saymagic'
                        name 'saymagic'
                        email 'saymagic.dev@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createPom {
    pom {
        project {
            packaging 'aar'

            name project.name
            description 'Android SDK for Bluefin server to support RxJava'
            url siteUrl
            inceptionYear '2016'

            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            developers {
                developer {
                    id 'saymagic'
                    name 'saymagic'
                    email 'saymagic.dev@gmail.com'
                }
            }
        }
    }.writeTo("$buildDir/poms/pom-default.xml").writeTo("pom.xml")
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

build.dependsOn createPom

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception e) {
    System.out.println("please add local.properties file")
}

bintray {
    user = properties.getProperty("bintray.user", System.getenv("BINTRAY_USER"))
    key =  properties.getProperty("bintray.apikey", System.getenv("BINTRAY_API_KEY"))
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "bluefin-rx-sdk"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
    }
}
